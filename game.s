	.OPT NOLIST,NOEJECT
;
;   GAME 1.0       
;   BY ROB McMULLEN
;
;   OS VARIABLES
;
DOSVEC	= $0A 
RTCLOK	= $14 
VDSLST	= $0200 
SDMCTL	= $022F 
SDLSTL	= $0230 
COLDST	= $0244 
GPRIOR	= $026F 
PADDL0	= $0270 
STICK0	= $0278 
PTRIG0	= $027C 
STRIG0	= $0284 
PCOLR0	= $02C0 
COLOR0	= $02C4 
COLORM	= $02C7 
RUNAD	= $02E0 
CHBAS	= $02F4 
CH	= $02FC 
HPOSP0	= $D000 
HPOSM0	= $D004 
SIZEP0	= $D008 
SIZEM	= $D00C 
COLPF0	= $D016 
GRACTL	= $D01D 
HITCLR	= $D01E 
CONSOL	= $D01F 
AUDF1	= $D200 
AUDC1	= $D201 
AUDCTL	= $D208 
RANDOM	= $D20A 
HSCROL	= $D404 
VSCROL	= $D405 
PMBASE	= $D407 
CHBASE	= $D409 
WSYNC	= $D40A 
NMIEN	= $D40E 
SETVBV	= $E45C 
SYSVBV	= $E45F 
XITVBV	= $E462 
;
;   ZERO PAGE VARS.
;
	*= $80 
VCOUNT	*= *+1 
DCOUNT	*= *+1 
XLOOP	*= *+1 
YLOOP	*= *+1 
CSHIFT	*= *+1 
LO	*= *+1 
HI	*= *+1 
COUNT	*= *+1 
COUNTR	*= *+1 
GUNDIR	*= *+1 
XSAVE	*= *+1 
YSAVE	*= *+1 
PLOTPOS	*= *+1 
PLOTX	*= *+1 
PLOTY	*= *+1 
PLOTCLR	*= *+1 
XCENTR	*= *+1 
YCENTR	*= *+1 
TEMP	*= *+1 
XTEMP	*= *+1 
YTEMP	*= *+1 
DCOLSV	*= *+1 
EXSCNT	*= *+1 
EXPCNT	*= *+1 
;
;   CONSTANTS
;
	*= $3000 
YPLOTL	*= *+200 
YPLOTH	*= *+200 
BACTIV	*= *+8 
BDIR	*= *+8 
BXPOSL	*= *+8 
BXPOS	*= *+8 
BYPOSL	*= *+8 
BYPOS	*= *+8 
ERALO	*= *+8 
ERAHI	*= *+8 
EACTIV	*= *+20 
EXPOS	*= *+20 
EYPOS	*= *+20 
ECOUNT	*= *+20 
PMLOC	= $3800 
PMM	= PMLOC+$0300 
PM0	= PMLOC+$0400 
PM1	= PMLOC+$0500 
PM2	= PMLOC+$0600 
PM3	= PMLOC+$0700 
SCRSCO	= $3800 
SCREEN	= $4100 
SCREEN2	= $5000 
;
	*= SCRSCO
	.SBYTE "       000000       "
	*= $02E0 
	.WORD INIT
	*= $6000 
;
;   INITIALIZATION
;
INIT
;   LDA # <INIT
;   STA DOSVEC
;   LDA # >INIT
;   STA DOSVEC+1
	LDA #<TITLEDL
	STA SDLSTL
	LDA #>TITLEDL
	STA SDLSTL+1 
	JSR TITLE
;
GAMEINIT
	LDA #<DLIST
	STA SDLSTL
	LDA #>DLIST
	STA SDLSTL+1 
	LDA #192 
	STA NMIEN
	LDA #<DLI
	STA VDSLST
	LDA #>DLI
	STA VDSLST+1 
	LDA #>PMLOC
	STA PMBASE
	LDA #62 
	STA SDMCTL
	LDA #3 
	STA GRACTL
	LDA #1 
	STA GPRIOR
	LDA #0 
	STA SIZEP0
	STA SIZEP0+1 
	STA SIZEP0+2 
	STA SIZEP0+3 
	STA SIZEM
	STA EXPCNT
	STA EXSCNT
	STA AUDCTL
	LDA #$86 
	STA PCOLR0
	LDA #$A8 
	STA PCOLR0+1 
	LDA #$0D 
	STA PCOLR0+3 
	LDA #$0A 
	STA DCOLSV
	LDA #0 
	TAY 
CLMEM	STA PMM,Y
	STA PM0,Y
	STA PM1,Y
	STA PM2,Y
	STA PM3,Y
	STA YPLOTL,Y
	STA YPLOTH,Y
	STA BACTIV,Y
	INY 
	BNE CLMEM
	LDX #0 
	LDA #<SCREEN
	STA LO
	LDA #>SCREEN
	STA HI
YADRL	LDA LO
	STA YPLOTL,X
	LDA HI
	STA YPLOTH,X
	LDA #0 
	TAY 
YADR1	STA (LO),Y
	INY 
	CPY #40 
	BCC YADR1
	CLC 
	LDA LO
	ADC #40 
	STA LO
	LDA HI
	ADC #0 
	STA HI
	INX 
	CPX #193 
	BCC YADRL
	LDA #7 	;DEFERRED
	LDX #>DVBLANK
	LDY #<DVBLANK
	JSR SETVBV
	LDA #6 	;IMMEDIATE
	LDX #>VBLANK
	LDY #<VBLANK
	JSR SETVBV
	JMP START
;
TITLEDL	.BYTE $70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$47 
	.WORD TITLESC
	.BYTE 6 ,$41 
	.WORD TITLEDL
;
TITLE	LDA #8 
	STA CONSOL
TTL0	LDA CONSOL
	CMP #6 
	BNE TTL0
	RTS 
;
TITLESC	.SBYTE +$80 ,"    METEOR STORM    "
	.SBYTE +$C0 ," ----PRESS START----"
;
DLI	PHA 
	LDA DCOLSV
	CLC 
	ADC #$10 
	STA DCOLSV
	STA COLPF0+2 
	PLA 
	RTI 
;
DLIST	.BYTE $70 ,$70 ,$46 
	.WORD SCRSCO
	.BYTE $CE 
	.WORD SCREEN
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,$4E 
	.WORD SCREEN2
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,$41 
	.WORD DLIST
;
;   PLAYER MISSLE SHAPES
;
PMGUN	.BYTE 0 ,0 ,0 ,0 ,0 ,$7E ,$7E ,$F0 
	.BYTE 0 ,0 ,0 ,0 ,$0E ,$7E ,$70 ,$F0 
	.BYTE 0 ,0 ,0 ,6 ,$1E ,$78 ,$60 ,$F0 
	.BYTE 0 ,0 ,6 ,$0E ,$3C ,$70 ,$60 ,$F0 
	.BYTE 0 ,6 ,$0E ,$1C ,$38 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$0C ,$1C ,$38 ,$30 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$18 ,$18 ,$38 ,$30 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$18 ,$38 ,$30 ,$30 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$30 ,$30 ,$30 ,$60 ,$60 ,$60 ,$F0 
	.BYTE 0 ,$30 ,$30 ,$60 ,$60 ,$60 ,$60 ,$F0 
	.BYTE 0 ,$60 ,$60 ,$60 ,$60 ,$60 ,$60 ,$F0 
	.BYTE 0 ,$0C ,$0C ,6 ,6 ,6 ,6 ,$0F 
	.BYTE 0 ,$0C ,$0C ,$0C ,6 ,6 ,6 ,$0F 
	.BYTE 0 ,$18 ,$1C ,$0C ,$0C ,$0E ,6 ,$0F 
	.BYTE 0 ,$18 ,$18 ,$1C ,$0C ,$0E ,6 ,$0F 
	.BYTE 0 ,$30 ,$38 ,$1C ,$0C ,$0E ,6 ,$0F 
	.BYTE 0 ,$60 ,$70 ,$38 ,$1C ,$0E ,6 ,$0F 
	.BYTE 0 ,0 ,$60 ,$70 ,$3C ,$0E ,6 ,$0F 
	.BYTE 0 ,0 ,0 ,$60 ,$78 ,$1E ,6 ,$0F 
	.BYTE 0 ,0 ,0 ,0 ,$70 ,$7E ,$0E ,$0F 
	.BYTE 0 ,0 ,0 ,0 ,0 ,$7E ,$7E ,$0F 
;
GUNMOVE
	LDY #126 
	LDA GUNDIR
	CMP #11 
	BCC GUN1
	LDY #122 
GUN1	STY HPOSP0+3 
	ASL A
	ASL A
	ASL A
	TAY 	;POS. IN TABLE
	LDX #189 
GUN2	LDA PMGUN,Y
	STA PM3,X
	INY 
	INX 
	CPX #197 
	BCC GUN2
	RTS 
;
START	LDA #10 
	STA GUNDIR
	JSR BASEDRAW
ST0	LDA #0 
	STA RTCLOK
ST01	LDA RTCLOK
	CMP #4 
	BCC ST01
	LDA STICK0
	CMP #11 
	BNE ST1
	INC GUNDIR
ST1	CMP #7 
	BNE ST2
	DEC GUNDIR
ST2	LDA GUNDIR
	BPL ST3
	LDA #0 
	STA GUNDIR
ST3	CMP #21 
	BCC ST4
	LDA #20 
	STA GUNDIR
ST4	LDA STRIG0+1 
	BNE ST41
	LDA RANDOM
	AND #127 
	STA XCENTR
	LDA RANDOM
	AND #127 
	STA YCENTR
	JSR NEWEXP
ST41	LDA STRIG0
	BNE ST0
	JSR BULREQ
	JMP ST0
;
BULREQ
	LDY #0 
BRQ1	LDA BACTIV,Y
	BEQ BRQ2
	INY 
	CPY #8 
	BCC BRQ1
	RTS 
BRQ2	LDA #$FF 
	STA BACTIV,Y
	LDA GUNDIR
	STA BDIR,Y
	LDA #79 
	STA BXPOS,Y
	LDA #162 
	STA BYPOS,Y
	LDA #0 
	STA BXPOSL,Y
	STA BYPOSL,Y
	RTS 
;
DVBLANK
	INC DCOUNT
	JSR GUNMOVE
	JSR BULLET
	JSR EXPHAN
	JSR SOUNDS
	JMP XITVBV
;
VBLANK
	INC VCOUNT
	AND #1 
	STA VCOUNT
	JMP SYSVBV
;
PLMASK	.BYTE $3F ,$CF ,$F3 ,$FC 
PLOTC0	.BYTE 0 ,0 ,0 ,0 
PLOTC1	.BYTE $40 ,$10 ,4 ,1 
PLOTC2	.BYTE $80 ,$20 ,8 ,2 
PLOTC3	.BYTE $C0 ,$30 ,$0C ,3 
;
XDIRHI	.BYTE 3 ,3 ,3 ,3 ,3 ,1 ,1 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,1 ,1 ,3 ,3 ,3 ,3 ,3 
XDIRLO	.BYTE 0 ,0 ,0 ,0 ,0 ,192 ,64 ,192 ,96 ,48 ,0 ,48 ,96 ,192 ,64 ,192 ,0 ,0 ,0 ,0 ,0 
YDIRHI	.BYTE 0 ,0 ,1 ,2 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,2 ,1 ,0 ,0 
YDIRLO	.BYTE 0 ,128 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,128 ,0 
;
BULLET
	LDY #7 
BUL0	LDA BACTIV,Y
	BNE BUL01
	JMP BULXX
BUL01	LDA ERAHI,Y
	STA HI
	LDA ERALO,Y
	STA LO
	LDA #0 
	TAX 
	STA (LO,X)
	LDX BDIR,Y
	CPX #11 
	BCS BUL1S
	LDA BXPOSL,Y
	CLC 
	ADC XDIRLO,X
	STA BXPOSL,Y
	LDA BXPOS,Y
	ADC XDIRHI,X
	STA BXPOS,Y
	JMP BUL2
BUL1S	LDA BXPOSL,Y
	SEC 
	SBC XDIRLO,X
	STA BXPOSL,Y
	LDA BXPOS,Y
	SBC XDIRHI,X
	STA BXPOS,Y
BUL2	CMP #160 
	BCS OUTBNDS
	AND #3 
	STA PLOTPOS
	LDA BXPOS,Y
	LSR A
	LSR A
	STA TEMP
	LDA BYPOSL,Y
	SEC 
	SBC YDIRLO,X
	STA BYPOSL,Y
	LDA BYPOS,Y
	SBC YDIRHI,X
	STA BYPOS,Y
	CMP #192 
	BCC BUL3
OUTBNDS
	LDA #0 
	STA BACTIV,Y
	BEQ BULXX
BUL3	TAX 
	LDA YPLOTL,X
	STA LO
	LDA YPLOTH,X
	STA HI
	CLC 
	LDA LO
	ADC TEMP
	STA LO
	STA ERALO,Y
	LDA HI
	ADC #0 
	STA HI
	STA ERAHI,Y
	LDX PLOTPOS
	LDA PLOTC3,X
	LDX #0 
	STA (LO,X)
BULXX	DEY 
	BMI BULRTS
	JMP BUL0
BULRTS	RTS 
;
GRAV	.BYTE 0 ,0 ,0 ,0 ,1 ,0 ,0 ,0 ,1 ,0 ,0 ,1 ,0 ,1 ,1 ,1 ,1 ,2 ,1 ,1 ,1 ,2 ,1 ,1 ,2 ,1 ,2 ,2 ,2 ,2 ,3 ,2 ,2 ,2 ,3 ,2 ,2 ,3 ,2 ,3 ,3 ,3 ,3 ,4 ,3 ,3 ,3 
;
PLOT
	LDA PLOTX
	CMP #160 
	BCS PLXXX
	LDA PLOTY
	CMP #192 
	BCS PLXXX
	LDA PLOTX
	AND #3 
	TAX 
	LDA PLOTX
	LSR A
	LSR A
	LDY PLOTY
	CLC 
	ADC YPLOTL,Y
	STA LO
	LDA YPLOTH,Y
	ADC #0 
	STA HI
	LDY #0 
	CLC 
PLXXX	RTS 
;
NEWEXP
	LDA #64 
	STA EXSCNT
	LDY #0 
NEW0	LDA EACTIV,Y
	BEQ NEW1
	INY 
	CPY #20 
	BCC NEW0
	RTS 
NEW1	LDA #$FF 
	STA EACTIV,Y
	LDA XCENTR
	STA EXPOS,Y
	LDA YCENTR
	STA EYPOS,Y
	LDA #0 
	STA ECOUNT,Y
RT1	RTS 
;
EXPHAN
	LDA #0 
	STA COUNTR
RUNLP	INC COUNTR
	LDX COUNTR
	CPX #20 
	BCS RT1
	LDA EACTIV,X
	BEQ RUNLP
	LDA #0 
	STA PLOTCLR
	LDA ECOUNT,X
	CMP #37 
	BMI DOPLOT
	INC PLOTCLR
	SEC 
	SBC #37 
	CMP #37 
	BMI DOPLOT
	LDA #0 
	STA EACTIV,X
	JMP RUNLP
DOPLOT	INC ECOUNT,X
	TAY 
	LDA EXPOS,X
	CLC 
	ADC XCOORD,Y
	STA PLOTX
	CMP #160 
	BCS RUNLP
	LDA EYPOS,X
	ADC YCOORD,Y
	STA PLOTY
	CMP #192 
	BCS RUNLP
	JSR PLOT
	LDA PLOTCLR
	BNE CLEARIT
	LDA (LO),Y
	AND PLMASK,X
	ORA PLOTC2,X
	STA (LO),Y
	TYA 
	CLC 
	ADC #40 
	TAY 
	LDA (LO),Y
	AND PLMASK,X
	ORA PLOTC2,X
	STA (LO),Y
	JMP RUNLP
CLEARIT	LDA (LO),Y
	AND PLMASK,X
	STA (LO),Y
	TYA 
	CLC 
	ADC #40 
	TAY 
	LDA (LO),Y
	AND PLMASK,X
	STA (LO),Y
	JMP RUNLP
;
XCOORD	.BYTE 0 ,1 ,255 ,0 ,255 ,1 
	.BYTE 0 ,2 ,255 ,254 ,0 ,1 
	.BYTE 0 ,254 ,2 ,1 ,1 ,255 
	.BYTE 0 ,2 ,254 ,255 ,3 ,0 
	.BYTE 253 ,254 ,3 ,2 ,255 ,254 
	.BYTE 1 ,255 ,3 ,253 ,1 ,253 ,2 
YCOORD	.BYTE 0 ,0 ,2 ,254 ,0 ,2 
	.BYTE 2 ,0 ,254 ,2 ,4 ,254 
	.BYTE 252 ,254 ,2 ,4 ,252 ,4 
	.BYTE 6 ,254 ,0 ,252 ,2 ,250 
	.BYTE 0 ,252 ,254 ,4 ,6 ,4 
	.BYTE 250 ,250 ,0 ,2 ,6 ,254 ,252 
;
BASEDRAW
	LDX #170 
BD0	LDY #0 
	LDA YPLOTL,X
	STA LO
	LDA YPLOTH,X
	STA HI
BD1	LDA #$55 
	STA (LO),Y
	INY 
	CPY #40 
	BCC BD1
	INX 
	CPX #192 
	BCC BD0
	RTS 
;
SOUNDS
	LDA EXSCNT
	BEQ NOSND
	LSR A
	LSR A
	STA AUDC1+6 
	LDA #$40 
	STA AUDF1+6 
	DEC EXSCNT
NOSND	RTS 
