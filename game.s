	.OPT NOLIST,NOEJECT
;
;   GAME 1.0       
;   BY ROB McMULLEN
;
;   OS VARIABLES
;
DOSVEC	= $0A 
RTCLOK	= $14 
VDSLST	= $0200 
SDMCTL	= $022F 
SDLSTL	= $0230 
COLDST	= $0244 
GPRIOR	= $026F 
PADDL0	= $0270 
STICK0	= $0278 
PTRIG0	= $027C 
STRIG0	= $0284 
PCOLR0	= $02C0 
COLOR0	= $02C4 
COLORM	= $02C7 
RUNAD	= $02E0 
CHBAS	= $02F4 
CH	= $02FC 
HPOSP0	= $D000 
HPOSM0	= $D004 
SIZEP0	= $D008 
SIZEM	= $D00C 
COLPF0	= $D016 
GRACTL	= $D01D 
HITCLR	= $D01E 
CONSOL	= $D01F 
AUDF1	= $D200 
AUDC1	= $D201 
RANDOM	= $D20A 
HSCROL	= $D404 
VSCROL	= $D405 
PMBASE	= $D407 
CHBASE	= $D409 
WSYNC	= $D40A 
NMIEN	= $D40E 
SETVBV	= $E45C 
SYSVBV	= $E45F 
XITVBV	= $E462 
;
;   ZERO PAGE VARS.
;
	*= $80 
VSTOP	*= *+1 
VCOUNT	*= *+1 
DCOUNT	*= *+1 
XLOOP	*= *+1 
YLOOP	*= *+1 
CSHIFT	*= *+1 
LO	*= *+1 
HI	*= *+1 
COUNT	*= *+1 
GUNDIR	*= *+1 
XSAVE	*= *+1 
YSAVE	*= *+1 
PLOTX	*= *+1 
PLOTY	*= *+1 
PLOTERA	*= *+1 
PLOTPOS	*= *+1 
PLOTADD	*= *+1 
PLOTYS	*= *+1 
TEMP	*= *+1 
XTEMP	*= *+1 
YTEMP	*= *+1 
XCENTR	*= *+1 
YCENTR	*= *+1 
DCOLSV	*= *+2 
;
;   CONSTANTS
;
	*= $3000 
YPLOTL	*= *+200 
YPLOTH	*= *+200 
BACTIV	*= *+8 
BDIR	*= *+8 
BXPOSL	*= *+8 
BXPOS	*= *+8 
BYPOSL	*= *+8 
BYPOS	*= *+8 
XPREQ	*= *+16 
XTIME	*= *+16 
XXPOS	*= *+128 
XYPOS	*= *+128 
XXDIR	*= *+128 
XYDIR	*= *+128 
XXADD	*= *+128 
XYADD	*= *+128 
XPEND	*= *+128 
PMLOC	= $3800 
PMM	= PMLOC+$0300 
PM0	= PMLOC+$0400 
PM1	= PMLOC+$0500 
PM2	= PMLOC+$0600 
PM3	= PMLOC+$0700 
SCRSCO	= $3800 
SCREEN	= $4100 
SCREEN2	= $5000 
;
	*= SCRSCO
	.SBYTE "       000000       "
	*= $02E0 
	.WORD INIT
	*= $6000 
;
;   INITIALIZATION
;
INIT
;   LDA # <INIT
;   STA DOSVEC
;   LDA # >INIT
;   STA DOSVEC+1
	JSR TITLE
;
GAMEINIT
	LDA #<DLIST
	STA SDLSTL
	LDA #>DLIST
	STA SDLSTL+1 
	LDA #192 
	STA NMIEN
	LDA #<DLI
	STA VDSLST
	LDA #>DLI
	STA VDSLST+1 
	LDA #>PMLOC
	STA PMBASE
	LDA #62 
	STA SDMCTL
	LDA #3 
	STA GRACTL
	LDA #1 
	STA GPRIOR
	LDA #0 
	STA SIZEP0
	STA SIZEP0+1 
	STA SIZEP0+2 
	STA SIZEP0+3 
	STA SIZEM
	LDA #$86 
	STA PCOLR0
	LDA #$A8 
	STA PCOLR0+1 
	LDA #$0D 
	STA PCOLR0+3 
	LDA #202 
	STA DCOLSV+1 
	LDA #$0A 
	STA DCOLSV
	LDA #0 
	TAY 
CLMEM	STA PMM,Y
	STA PM0,Y
	STA PM1,Y
	STA PM2,Y
	STA PM3,Y
	STA YPLOTL,Y
	STA YPLOTH,Y
	STA BACTIV,Y
	INY 
	BNE CLMEM
	LDX #0 
	LDA #<SCREEN
	STA LO
	LDA #>SCREEN
	STA HI
YADRL	LDA LO
	STA YPLOTL,X
	LDA HI
	STA YPLOTH,X
	LDA #0 
	TAY 
YADR1	STA (LO),Y
	INY 
	CPY #40 
	BCC YADR1
	CLC 
	LDA LO
	ADC #40 
	STA LO
	LDA HI
	ADC #0 
	STA HI
	INX 
	CPX #193 
	BCC YADRL
	LDA #7 	;DEFERRED
	LDX #>DVBLANK
	LDY #<DVBLANK
	JSR SETVBV
	LDA #6 	;IMMEDIATE
	LDX #>VBLANK
	LDY #<VBLANK
	JSR SETVBV
	LDA #1 
	STA VSTOP
	JMP START
;
TITLEDL	.BYTE $70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$47 
	.WORD TITLESC
	.BYTE $70 ,6 ,$70 ,$70 ,6 ,$70 ,$70 ,$70 ,6 ,$70 ,$70 ,$70 ,$70 
	.BYTE 6 ,$41 
	.WORD TITLEDL
;
TITLESC
	.SBYTE +$C0 ,"     FREE FALL!     "
	.SBYTE +$80 ,"  THE METEOR STORM  "
	.SBYTE "  BY  ROB MCMULLEN  "
	.SBYTE "ANALOG COMPUTING #60"
	.SBYTE +$40 ,"     PRESS START    "
;
TITLE	LDA #146 
	STA COLOR0
	LDA #144 
	STA COLOR0+1 
	LDA #148 
	STA COLOR0+2 
	LDA #150 
	STA COLOR0+3 
	LDA #<TITLEDL
	STA SDLSTL
	LDA #>TITLEDL
	STA SDLSTL+1 
	LDA #8 
	STA CONSOL
TTLOOP	LDA CONSOL
	CMP #6 
	BNE TTLOOP
	RTS 
;
DLI	PHA 
	LDA DCOLSV
	CLC 
	ADC #$10 
	STA DCOLSV
	STA COLPF0+2 
	LDA DCOLSV+1 
	EOR #202 
	STA DCOLSV+1 
	STA COLPF0+1 
	PLA 
	RTI 
;
DLIST	.BYTE $70 ,$70 ,$46 
	.WORD SCRSCO
	.BYTE $CE 
	.WORD SCREEN
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,$4E 
	.WORD SCREEN2
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,14 ,14 ,14 
	.BYTE 14 ,14 ,14 ,14 ,$41 
	.WORD DLIST
;
;   PLAYER MISSLE SHAPES
;
PMGUN	.BYTE 0 ,0 ,0 ,0 ,0 ,$7E ,$7E ,$F0 
	.BYTE 0 ,0 ,0 ,0 ,$0E ,$7E ,$70 ,$F0 
	.BYTE 0 ,0 ,0 ,6 ,$1E ,$78 ,$60 ,$F0 
	.BYTE 0 ,0 ,6 ,$0E ,$3C ,$70 ,$60 ,$F0 
	.BYTE 0 ,6 ,$0E ,$1C ,$38 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$0C ,$1C ,$38 ,$30 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$18 ,$18 ,$38 ,$30 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$18 ,$38 ,$30 ,$30 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$30 ,$30 ,$30 ,$60 ,$60 ,$60 ,$F0 
	.BYTE 0 ,$30 ,$30 ,$60 ,$60 ,$60 ,$60 ,$F0 
	.BYTE 0 ,$60 ,$60 ,$60 ,$60 ,$60 ,$60 ,$F0 
	.BYTE 0 ,$0C ,$0C ,6 ,6 ,6 ,6 ,$0F 
	.BYTE 0 ,$0C ,$0C ,$0C ,6 ,6 ,6 ,$0F 
	.BYTE 0 ,$18 ,$1C ,$0C ,$0C ,$0E ,6 ,$0F 
	.BYTE 0 ,$18 ,$18 ,$1C ,$0C ,$0E ,6 ,$0F 
	.BYTE 0 ,$30 ,$38 ,$1C ,$0C ,$0E ,6 ,$0F 
	.BYTE 0 ,$60 ,$70 ,$38 ,$1C ,$0E ,6 ,$0F 
	.BYTE 0 ,0 ,$60 ,$70 ,$3C ,$0E ,6 ,$0F 
	.BYTE 0 ,0 ,0 ,$60 ,$78 ,$1E ,6 ,$0F 
	.BYTE 0 ,0 ,0 ,0 ,$70 ,$7E ,$0E ,$0F 
	.BYTE 0 ,0 ,0 ,0 ,0 ,$7E ,$7E ,$0F 
;
GUNMOVE
	LDY #126 
	LDA GUNDIR
	CMP #11 
	BCC GUN1
	LDY #122 
GUN1	STY HPOSP0+3 
	ASL A
	ASL A
	ASL A
	TAY 	;POS. IN TABLE
	LDX #199 
GUN2	LDA PMGUN,Y
	STA PM3,X
	INY 
	INX 
	CPX #207 
	BCC GUN2
	RTS 
;
START	LDA #10 
	STA GUNDIR
ST0	LDA #0 
	STA RTCLOK
ST01	LDA RTCLOK
	CMP #4 
	BCC ST01
	LDA STICK0
	CMP #11 
	BNE ST1
	INC GUNDIR
ST1	CMP #7 
	BNE ST2
	DEC GUNDIR
ST2	LDA GUNDIR
	BPL ST3
	LDA #0 
	STA GUNDIR
ST3	CMP #21 
	BCC ST4
	LDA #20 
	STA GUNDIR
ST4	LDA STRIG0+1 
	BNE ST41
	LDA #80 
	STA XCENTR
	LDA #50 
	STA YCENTR
	JSR EXPREQ
ST41	LDA STRIG0
	BNE ST0
	LDY #0 
ST5	LDA BACTIV,Y
	BEQ ST6
	INY 
	CPY #8 
	BCC ST5
	BCS ST0
ST6	LDA #$FF 
	STA BACTIV,Y
	LDA GUNDIR
	STA BDIR,Y
	LDA #79 
	STA BXPOS,Y
	LDA #172 
	STA BYPOS,Y
	LDA #0 
	STA BXPOSL,Y
	STA BYPOSL,Y
	JMP ST0
;
DVBLANK
	LDA VSTOP
	BEQ DVEXIT
	INC DCOUNT
	JSR BULLET
	JSR EXPLODE
DVEXIT	JMP XITVBV
;
VBLANK
	LDA VSTOP
	BEQ VEXIT
	INC VCOUNT
	JSR GUNMOVE
VEXIT	JMP SYSVBV
;
PLMASK	.BYTE $3F ,$CF ,$F3 ,$FC 
PLCOLR	.BYTE 0 ,0 ,0 ,0 
	.BYTE $40 ,$10 ,4 ,1 
	.BYTE $80 ,$20 ,8 ,2 
	.BYTE $C0 ,$30 ,$0C ,3 
;
XDIRHI	.BYTE 3 ,3 ,3 ,3 ,3 ,1 ,1 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,1 ,1 ,3 ,3 ,3 ,3 ,3 
XDIRLO	.BYTE 0 ,0 ,0 ,0 ,0 ,192 ,64 ,192 ,96 ,48 ,0 ,48 ,96 ,192 ,64 ,192 ,0 ,0 ,0 ,0 ,0 
YDIRHI	.BYTE 0 ,0 ,1 ,2 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,3 ,2 ,1 ,0 ,0 
YDIRLO	.BYTE 0 ,128 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 ,128 ,0 
;
BULLET
	LDY #7 
BUL0	LDA BACTIV,Y
	BNE BUL01
	JMP BULXX
BUL01	LDA BXPOS,Y
	STA PLOTX
	LDA BYPOS,Y
	STA PLOTY
	LDA #0 
	JSR PLOT
	LDX BDIR,Y
	CPX #11 
	BCS BUL1S
	LDA BXPOSL,Y
	CLC 
	ADC XDIRLO,X
	STA BXPOSL,Y
	LDA BXPOS,Y
	ADC XDIRHI,X
	STA BXPOS,Y
	JMP BUL2
BUL1S	LDA BXPOSL,Y
	SEC 
	SBC XDIRLO,X
	STA BXPOSL,Y
	LDA BXPOS,Y
	SBC XDIRHI,X
	STA BXPOS,Y
BUL2	STA PLOTX
	LDA BYPOSL,Y
	SEC 
	SBC YDIRLO,X
	STA BYPOSL,Y
	LDA BYPOS,Y
	SBC YDIRHI,X
	STA BYPOS,Y
	STA PLOTY
	LDA #12 
	JSR PLOT
	BCS OUTBNDS
BULXX	DEY 
	BPL BUL0
	RTS 
OUTBNDS
	LDA #0 
	STA BACTIV,Y
	BEQ BULXX
;
EXPREQ
	LDY #0 
EXR1	LDA XPREQ,Y
	BEQ EXPSET
	INY 
	CPY #8 
	BCC EXR1
	RTS 
EXPSET
	LDA #$FF 
	STA XPREQ,Y
	LDA #0 
	STA XTIME,Y
	TYA 
	ASL A
	ASL A
	ASL A
	ASL A
	TAX 
	LDY #0 
EXR2	LDA XCENTR
	STA XXPOS,X
	LDA YCENTR
	STA XYPOS,X
	LDA XDINI,Y
	STA XXDIR,X
	LDA YDINI,Y
	STA XYDIR,X
	LDA #0 
	STA XXADD,X
	STA XYADD,X
	LDA RANDOM
	AND #15 
	CLC 
	ADC #18 
	STA XPEND,X
	INX 
	INY 
	CPY #16 
	BCC EXR2
	RTS 
;
XDINI	.BYTE 2 ,2 ,2 ,1 ,0 ,-1 ,-2 ,-2 ,-2 ,-2 ,-2 ,-1 ,0 ,1 ,2 ,2 
YDINI	.BYTE 0 ,-1 ,-2 ,-2 ,-2 ,-2 ,-2 ,-1 ,0 ,1 ,2 ,2 ,2 ,2 ,2 ,1 
GRAV	.BYTE 0 ,0 ,0 ,0 ,1 ,0 ,0 ,0 ,1 ,0 ,0 ,1 ,0 ,1 ,1 ,1 ,1 ,2 ,1 ,1 ,1 ,2 ,1 ,1 ,2 ,1 ,2 ,2 ,2 ,2 ,3 ,2 ,2 ,2 ,3 ,2 ,2 ,3 ,2 ,3 ,3 ,3 ,3 ,4 ,3 ,3 ,3 
;
EXPLODE
	LDA DCOUNT
	AND #1 
	TAY 
EXP0	STY YSAVE
	LDA XPREQ,Y
	BEQ EXPXX
	TYA 
	ASL A
	ASL A
	ASL A
	ASL A
	TAX 
	LDY #0 
EXP1	STY YLOOP
	LDA XXPOS,X
	STA PLOTX
	LDA XYPOS,X
	STA PLOTY
	AND PLOTX
	CMP #$FF 
	BEQ EXPL
	LDA #0 
	JSR PLOT
EXP12	LDY YSAVE
	LDA XTIME,Y
	CMP XPEND,X
	BCS XOUT
	TAY 
	LDA XXPOS,X
	CLC 
	ADC XXDIR,X
	STA XXPOS,X
	STA PLOTX
	LDA XYPOS,X
	CLC 
	ADC XYDIR,X
	CLC 
	ADC GRAV,Y
	STA XYPOS,X
	STA PLOTY
	LDA #8 
	JSR PLOT
	BCS XOUT
EXPL	LDY YLOOP
	INX 
	INY 
	CPY #16 
	BCC EXP1
EXPXX	LDY YSAVE
	LDX XTIME,Y
	CPX #36 
	BCS XSTOP
	INX 
	TXA 
	STA XTIME,Y
EXPXXX	INY 
	INY 
	CPY #6 
	BCC EXP0
	RTS 
XSTOP	LDY YSAVE
	LDA #0 
	STA XPREQ,Y
	JMP EXPXXX
XOUT	LDA #$FF 
	STA XXPOS,X
	STA XYPOS,X
	JMP EXPL
;
PLOT
	STA PLOTADD
	LDA PLOTX
	CMP #160 
	BCS PLXXX
	LDA PLOTY
	CMP #192 
	BCS PLXXX
	STY PLOTYS
	LDA PLOTX
	AND #3 
	STA PLOTERA
	CLC 
	ADC PLOTADD
	STA PLOTPOS
	LDA PLOTX
	LSR A
	LSR A
	STA PLOTADD
	LDY PLOTY
	LDA YPLOTL,Y
	CLC 
	ADC PLOTADD
	STA LO
	LDA YPLOTH,Y
	ADC #0 
	STA HI
	LDY #0 
	LDA (LO),Y
	LDY PLOTERA
	AND PLMASK,Y
	LDY PLOTPOS
	ORA PLCOLR,Y
	LDY #0 
	STA (LO),Y
	LDY PLOTYS
	CLC 
PLXXX	RTS 
