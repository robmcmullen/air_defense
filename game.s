	.OPT NOLIST,OBJ
	.OPT NOEJECT
;
;   GAME 1.0       
;   BY ROB McMULLEN
;
;   OS VARIABLES
;
DOSVEC	= $0A 
RTCLOK	= $14 
VDSLST	= $0200 
SDMCTL	= $022F 
SDLSTL	= $0230 
COLDST	= $0244 
GPRIOR	= $026F 
PADDL0	= $0270 
STICK0	= $0278 
PTRIG0	= $027C 
STRIG0	= $0284 
PCOLR0	= $02C0 
COLOR0	= $02C4 
COLORM	= $02C7 
RUNAD	= $02E0 
CHBAS	= $02F4 
CH	= $02FC 
HPOSP0	= $D000 
HPOSM0	= $D004 
SIZEP0	= $D008 
SIZEM	= $D00C 
COLPF0	= $D016 
GRACTL	= $D01D 
HITCLR	= $D01E 
COLSOL	= $D01F 
AUDF1	= $D200 
AUDC1	= $D201 
RANDOM	= $D20A 
HSCROL	= $D404 
VSCROL	= $D405 
PMBASE	= $D407 
CHBASE	= $D409 
WSYNC	= $D40A 
NMIEN	= $D40E 
SETVBV	= $E45C 
SYSVBV	= $E45F 
XITVBV	= $E462 
;
;   ZERO PAGE VARS.
;
	*= $80 
XCOUNT	*= *+1 
YCOUNT	*= *+1 
CSHIFT	*= *+1 
LO	*= *+1 
HI	*= *+1 
COUNT	*= *+1 
GUNDIR	*= *+1 
;
;   CONSTANTS
;
PMLOC	= $4000 
PMM	= PMLOC+$0300 
PM0	= PMLOC+$0400 
PM1	= PMLOC+$0500 
PM2	= PMLOC+$0600 
PM3	= PMLOC+$0700 
TITLESC	= PMM
SCRSCO	= $4100 
SCREEN	= $4800 
;
	*= SCRSCO
	.SBYTE " 000000"
	*= $6000 
;
;   INITIALIZATION
;
INIT
;   LDA # <INIT
;   STA DOSVEC
;   LDA # >INIT
;   STA DOSVEC+1
	LDA #<TITLEDL
	STA SDLSTL
	LDA #>TITLEDL
	STA SDLSTL+1 
	JSR TITLE
;
GAMEINIT
	LDA #<DLIST
	STA SDLSTL
	LDA #>DLIST
	STA SDLSTL+1 
;   LDA #192
;   STA NMIEN
;   LDA # <DLI
;   STA VDSLST
;   LDA # >DLI
;   STA VDSLST+1
	LDA #>PMLOC
	STA PMBASE
	LDA #62 
	STA SDMCTL
	LDA #3 
	STA GRACTL
	LDA #1 
	STA GPRIOR
	LDA #0 
	STA SIZEP0
	STA SIZEP0+1 
	STA SIZEP0+2 
	STA SIZEP0+3 
	STA SIZEM
	LDA #$86 
	STA PCOLR0
	LDA #$A8 
	STA PCOLR0+1 
	LDA #$0D 
	STA PCOLR0+3 
	LDA #0 
	TAY 
CLRPM	STA PMM,Y
	STA PM0,Y
	STA PM1,Y
	STA PM2,Y
	STA PM3,Y
	INY 
	BNE CLRPM
;   LDA #7      ;DEFERRED
;   LDX # >DVBLANK
;   LDY # <DVBLANK
;   JSR SETVBV
;   LDA #6      ;IMMEDIATE
;   LDX # >VBLANK
;   LDY # <VBLANK
;   JSR SETVBV
	JMP START
;
TITLEDL	.BYTE $70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 ,$70 
TTLDLSZ	.BYTE $46 
	.WORD TITLESC
	.BYTE 6 ,$41 
	.WORD TITLEDL
;
TITLE	LDA #0 
	STA CSHIFT
	STA XCOUNT
	LDA #60 
	STA YCOUNT
	JSR DELAY
TTLOOP	LDA #140 
	STA YCOUNT
	LDA #15 
	STA COLOR0
	LDY #39 
	LDA #0 
TTL0	STA TITLESC,Y
	DEY 
	BPL TTL0
	LDY #$46 
	LDA XCOUNT
	CMP #1 
	BNE TTL01
	INY 
TTL01	STY TTLDLSZ
	LDY #0 
	LDX CSHIFT
TTL1	LDA CREDITS,X
	CMP #$FF 
	BEQ TTL1A
	STA TITLESC,Y
	INX 
	INY 
	BNE TTL1
TTL1A	INX 
	STX CSHIFT
	INC XCOUNT
	JSR DELAY
	LDA #2 
	STA YCOUNT
TTL2	JSR DELAY
	DEC COLOR0
	BNE TTL2
	LDA #60 
	STA YCOUNT
	JSR DELAY
	LDA XCOUNT
	CMP #3 
	BCC TTLOOP
	RTS 
;
DELAY	LDA #0 
	STA RTCLOK
DEL1	LDA RTCLOK
	CMP YCOUNT
	BCC DEL1
	RTS 
;
CREDITS	.SBYTE "  ANALOG COMPUTING  "
	.SBYTE "      PRESENTS:"
	.SBYTE " AIR DEFENSE COMMAND"
	.SBYTE "   BY ROB MCMULLEN"
;
DLIST	.BYTE $70 ,$70 ,$46 
	.WORD SCRSCO
	.BYTE $4D 
	.WORD SCREEN
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,13 ,13 
	.BYTE 13 ,13 ,13 ,13 ,13 ,$41 
	.WORD DLIST
;
;   PLAYER MISSLE SHAPES
;
PMGUN	.BYTE 0 ,0 ,0 ,0 ,0 ,$7E ,$7E ,$F0 
	.BYTE 0 ,0 ,0 ,0 ,$0E ,$7E ,$70 ,$F0 
	.BYTE 0 ,0 ,0 ,6 ,$1E ,$78 ,$60 ,$F0 
	.BYTE 0 ,0 ,6 ,$0E ,$3C ,$70 ,$60 ,$F0 
	.BYTE 0 ,6 ,$0E ,$1C ,$38 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$0C ,$1C ,$38 ,$30 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$18 ,$18 ,$38 ,$30 ,$70 ,$60 ,$F0 
	.BYTE 0 ,$30 ,$30 ,$30 ,$60 ,$60 ,$60 ,$F0 
	.BYTE 0 ,$60 ,$60 ,$60 ,$60 ,$60 ,$60 ,$F0 
	.BYTE 0 ,$0C ,$0C ,$0C ,6 ,6 ,6 ,$0F 
	.BYTE 0 ,$18 ,$18 ,$1C ,$0C ,$0E ,6 ,$0F 
	.BYTE 0 ,$30 ,$38 ,$1C ,$0C ,$0E ,6 ,$0F 
	.BYTE 0 ,$60 ,$70 ,$38 ,$1C ,$0E ,6 ,$0F 
	.BYTE 0 ,0 ,$60 ,$70 ,$3C ,$0E ,6 ,$0F 
	.BYTE 0 ,0 ,0 ,$60 ,$78 ,$1E ,6 ,$0F 
	.BYTE 0 ,0 ,0 ,0 ,$70 ,$7E ,$0E ,$0F 
	.BYTE 0 ,0 ,0 ,0 ,0 ,$7E ,$7E ,$0F 
;
GUNMOVE
	LDY #126 
	LDA GUNDIR
	CMP #9 
	BCC GUN1
	LDY #122 
GUN1	STY HPOSP0+3 
	ASL A
	ASL A
	ASL A
	TAY 	;POS. IN TABLE
	LDX #200 
GUN2	LDA PMGUN,Y
	STA PM3,X
	INY 
	INX 
	CPX #208 
	BCC GUN2
	RTS 
;
START	LDA #8 
	STA GUNDIR
	JSR GUNMOVE
ST0	LDA #0 
	STA RTCLOK
ST01	LDA RTCLOK
	CMP #4 
	BCC ST01
	LDA STICK0
	CMP #11 
	BNE ST1
	INC GUNDIR
ST1	CMP #7 
	BNE ST2
	DEC GUNDIR
ST2	LDA GUNDIR
	BPL ST3
	LDA #0 
	STA GUNDIR
ST3	CMP #17 
	BCC ST4
	LDA #16 
	STA GUNDIR
ST4	JSR GUNMOVE
	JMP ST0
